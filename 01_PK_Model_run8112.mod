;; 1. Based on: run8111
;; 2. Description: PK HDIT101, 2-cmt, IIV CL+V+VP
;; x1. Author: Och

;; 3. Label:

$PROBLEM PK
$INPUT ID USUBJID=DROP TIME DV OBS CMT MDV EVID AMT RATE AGE SEX RACE HEIGHTBL WEIGHTBL BMIBL STUDY ALAT ALBUMIN ASAT BILIRUBIN GFR GGT PROTEIN ADA ADA_MRD ADA_NA ALAT_BASE ALBUMIN_BASE ASAT_BASE BILIRUBIN_BASE GFR_BASE GGT_BASE PROTEIN_BASE ADA_BASE BSA SITEID TIME_PRIOR_RAND EPISODES_LAST SMOKER_NO CONTRACEPTIVE_DRUG
$DATA     ..\DATASET\HDIT101_PK_V02.csv IGNORE=@

$SUBROUTINES ADVAN6 TOL=5

$MODEL
COMP = (CENTRAL)
COMP = (PERI)

$PK
CL	= THETA(1) *EXP(ETA(1))*((WEIGHTBL/72)**THETA(6))
V	= THETA(2)*EXP(ETA(2))*((HEIGHTBL/174)**THETA(5))*(THETA(9)**(STUDY-1))
Q   = THETA(3)
VP  = THETA(4)*EXP(ETA(3))*(THETA(8)**SEX)*((1+THETA(7)*(TIME_PRIOR_RAND-2.8598))) 
S1	= V 


$DES
DADT(1) = - CL/V*A(1) - Q/V*A(1) + Q/VP*A(2)
DADT(2) =               Q/V*A(1) - Q/VP*A(2)

$ERROR
IPRED = F
DEL =0
IF (IPRED.EQ.0) DEL=0.0001
W = F
IRES = DV-IPRED
IWRES = IRES/(W+DEL)
Y= IPRED + EPS(1) + W*EPS(2)

$THETA
(0, 0.01)    ; 1 CL
(0, 3.03)    ; 2 VC
(0, 0.013)   ; 3 Q
(0, 2.71)    ; 4 VP
(0, 2.12)    ; 5 COV (HEIGHT)
(0, 0.654)   ; 6 COV (WEIGHT)
(0, 0.0178)  ; 7 COV (DISEASE DURATION)
(0, 0.728)   ; 8 COV (SEX)
(0, 1.13)    ; 9 COV (STUDY)

$OMEGA  
 0.0366   ; IIV CL
 0.0126   ; IIV V
 0 FIX    ; IIV VP

$SIGMA
 1.92     ; Add RE
 0.00905  ; Prop RE

$EST METHOD=1 INTER MAXEVAl=9999 NOABORT PRINT=1 SIG=3 POSTHOC
$COV
$TABLE ID TIME ETAS(1:LAST) AGE SEX RACE BMIBL HEIGHTBL WEIGHTBL STUDY IPRED IWRES CWRES NOPRINT ONEHEADER FILE=sdtab8112
